plugins {
    id 'java'
    id 'org.springframework.boot' version '2.1.7.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'jacoco'
    id 'com.ewerk.gradle.plugins.querydsl' version '1.0.10'
}

group = 'io.joonak'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
    implementation.exclude module: 'spring-boot-starter-logging'
    testImplementation.exclude module: 'junit'
}

repositories {
    mavenCentral()
}

def querydslDestDir = "build/generated/sources/apt/main"

querydsl {
    jpa = true
    library = 'com.querydsl:querydsl-apt'
    querydslSourcesDir = querydslDestDir
}

configurations {
    querydsl.extendsFrom compileClasspath
}

compileQuerydsl {
    options.annotationProcessorPath = configurations.querydsl
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', querydslDestDir]
        }
    }
    querydsl {
        resources {
            srcDirs = ['build/generated/sources/apt/resources']
        }
    }
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
        html.destination file("${buildDir}/jacoco/html")
    }
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/Q*.class'])
        })
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-log4j2'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.integration:spring-integration-ftp'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.0.1'

    implementation 'com.querydsl:querydsl-jpa'
    compileOnly 'com.querydsl:querydsl-apt'

    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'

    runtimeOnly 'mysql:mysql-connector-java'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-params'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.springframework.security:spring-security-test'
}
